plugins {
	id 'jqwik.common-configuration'
	id 'jqwik.module-configuration'
}

description = "Jqwik Aggregator module"

jar {
	archiveBaseName.set('jqwik')
	archiveVersion.set("${jqwikVersion}")
}

publishing {
	publications {
		jqwik(MavenPublication) {
			groupId = 'net.jqwik'
			artifactId = 'jqwik'
			from components.java
			pom {
				groupId = 'net.jqwik'
				name = 'jqwik'
				description = project.description
				url = 'https://jqwik.net/'
				licenses {
					license {
						name = 'Eclipse Public License - v 2.0'
						url = 'http://www.eclipse.org/legal/epl-v20.html'
					}
				}
				developers {
					developer {
						id = 'jlink'
						name = 'Johannes Link'
						email = 'business@johanneslink.net'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/jqwik-team/jqwik.git'
					developerConnection = 'scm:git:git://github.com/jqwik-team/jqwik.git'
					url = 'https://github.com/jqwik-team/jqwik'
				}
			}
		}
	}
}

signing {
	if (!isSnapshotRelease) {
		sign publishing.publications.jqwik
	}
}

dependencies {
	api(project(":api"))
	api(project(":web"))
	api(project(":time"))
	runtimeOnly(project(":engine"))
}
