ext {
	junitPlatformVersion = '1.3.2'
	junitJupiterVersion = '5.3.2'
	moduleName = 'net.jqwik'
	jqwikVersion = '1.0.0'
}

allprojects {
	repositories {
		mavenCentral()
		//maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
	}

	group = moduleName
	version = jqwikVersion

	apply plugin: 'java-library'
	apply plugin: 'maven-publish'
	apply plugin: 'signing'

	task javadocJar(type: Jar) {
		classifier = 'javadoc'
		from javadoc
	}

	task sourcesJar(type: Jar) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	tasks['publish'].dependsOn(build)

	publishing {
		repositories {
			maven {
				def ossrhUsername = project.hasProperty('ossrhUsername') ? project.ossrhUsername : ''
				def ossrhPassword = project.hasProperty('ossrhPassword') ? project.ossrhPassword : ''

				credentials {
					username = ossrhUsername
					password = ossrhPassword
				}

				// change URLs to point to your repos, e.g. http://my.org/repo
				def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
				def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
				url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
			}
		}
	}

	compileTestJava {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
		options.compilerArgs += '-parameters'
		options.encoding = 'UTF-8'
	}

	dependencies {
		api("org.apiguardian:apiguardian-api:1.0.0")
	}
}

wrapper {
	description = 'Generates gradlew[.bat] scripts'
	gradleVersion = '5.1'
}
