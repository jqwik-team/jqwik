static isSnapshotRelease(versionString) {
	versionString.endsWith('SNAPSHOT')
}

ext {
	junitPlatformVersion = '1.7.0'
	junitJupiterVersion = '5.7.0'
	apiGuardianVersion = '1.1.0'
	opentest4jVersion = '1.2.0'
	archunitVersion = '0.13.1'
	assertJVersion = '3.17.2'
	mockitoVersion = '3.5.10'
	systemLambdaVersion = '1.1.0'
	moduleName = 'net.jqwik'
	jqwikVersion = '1.4.0-SNAPSHOT'
	isSnapshotRelease = isSnapshotRelease(jqwikVersion)
}

allprojects {
	repositories {
		mavenCentral()
		//maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
	}

	group = moduleName
	version = jqwikVersion

	apply plugin: 'java-library'
	apply plugin: 'maven-publish'
	apply plugin: 'signing'

	tasks['publish'].dependsOn(build)

	publishing {
		repositories {
			maven {
				// hint: credentials are in ~/.gradle/gradle.properties
				def ossrhUsername = project.hasProperty('ossrhUsername') ? project.ossrhUsername : ''
				def ossrhPassword = project.hasProperty('ossrhPassword') ? project.ossrhPassword : ''

				credentials {
					username = ossrhUsername
					password = ossrhPassword
				}

				// change URLs to point to your repos, e.g. http://my.org/repo
				def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
				def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
				url = isSnapshotRelease ? snapshotsRepoUrl : releasesRepoUrl
			}
		}
	}

	java {
		withJavadocJar()
		withSourcesJar()
		sourceCompatibility = JavaVersion.VERSION_1_8
		targetCompatibility = JavaVersion.VERSION_1_8
	}

	compileTestJava {
		options.compilerArgs += '-parameters'
		options.encoding = 'UTF-8'
	}

	dependencies {
		api("org.apiguardian:apiguardian-api:${apiGuardianVersion}")
	}

}

wrapper {
	gradleVersion = '6.7.1'
}

