plugins {
	id 'jqwik.common-configuration'
}

description = "Jqwik Testing support module"

jar {
	archiveBaseName.set('jqwik-testing')
	archiveVersion.set("${jqwikVersion}")
}

moduleConfig {
	moduleInfoPath = 'src/main/module/module-info.java' // don't confuse IDEs
	version = "${jqwikVersion}"
	neverCompileModuleInfo = true
}

task publishTestingJavadoc(type: Javadoc) {
	source = file('./src/main/java')
	failOnError = false
	destinationDir = file("${rootDir}/docs/javadoc")
	include '**/testing/**'
}

publishing {
	publications {
		jqwikTesting(MavenPublication) {
			groupId = 'net.jqwik'
			artifactId = 'jqwik-testing'
			from components.java
			pom {
				groupId = 'net.jqwik'
				name = 'jqwik-testing'
				description = project.description
				url = 'https://jqwik.net/'
				licenses {
					license {
						name = 'Eclipse Public License - v 2.0'
						url = 'http://www.eclipse.org/legal/epl-v20.html'
					}
				}
				developers {
					developer {
						id = 'jlink'
						name = 'Johannes Link'
						email = 'business@johanneslink.net'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/jlink/jqwik.git'
					developerConnection = 'scm:git:git://github.com/jlink/jqwik.git'
					url = 'https://github.com/jlink/jqwik'
				}
			}
		}
	}
}

signing {
	if (!isSnapshotRelease) {
		sign publishing.publications.jqwikTesting
	}
}

dependencies {
	api(project(":api"))
	api("org.opentest4j:opentest4j:${opentest4jVersion}")

	implementation("org.mockito:mockito-core:${mockitoVersion}")
	implementation("org.assertj:assertj-core:${assertJVersion}")
}
