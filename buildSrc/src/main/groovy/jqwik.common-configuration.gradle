plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
	// See https://github.com/vlsi/vlsi-release-plugins/blob/master/plugins/gradle-extensions-plugin/README.md
	id 'com.github.vlsi.gradle-extensions'
}

repositories {
	mavenCentral()
	//maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
}

group = moduleName
version = jqwikVersion

tasks['publish'].dependsOn(build)

publishing {
	repositories {
		maven {
			// hint: credentials are in ~/.gradle/gradle.properties
			// Since June 2025 there is a new portal which requires new tokens
			def repoUsername = project.hasProperty('portalTokenUsername') ? project.portalTokenUsername : ''
			def repoPassword = project.hasProperty('portalTokenPassword') ? project.portalTokenPassword : ''

			credentials {
				username = repoUsername
				password = repoPassword
			}

			// Documentation for new publishing mechanism: https://central.sonatype.org/publish/publish-portal-ossrh-staging-api/
			// URLs suggested in https://central.sonatype.org/publish/publish-portal-ossrh-staging-api/#configuring-your-plugin

			// Works but deployments don't show up in https://central.sonatype.com
			def releasesRepoUrl = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"

			// Results in 400 error:
			// def releasesRepoUrl = "https://ossrh-staging-api.central.sonatype.com/service/local/"

			// Works
			def snapshotsRepoUrl = "https://central.sonatype.com/repository/maven-snapshots/"

			url = isSnapshotRelease ? snapshotsRepoUrl : releasesRepoUrl

			// Release versions require an additional request to be executed for manual deployments:
			/*
			curl -X 'POST' \
			  'https://ossrh-staging-api.central.sonatype.com/manual/upload/defaultRepository/net.jqwik' \
			  -H 'accept: application/json' \
			  -H 'Authorization: Bearer <BASE64 token>'
			*/

		}
	}
}

java {
	withJavadocJar()
	withSourcesJar()
	sourceCompatibility = JavaVersion.toVersion("${javaTargetVersion}")
	targetCompatibility = JavaVersion.toVersion("${javaTargetVersion}")
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

compileTestJava {
	options.compilerArgs += '-parameters'
}

dependencies {
	api("org.apiguardian:apiguardian-api:${apiGuardianVersion}")
	compileOnly("org.jspecify:jspecify:${jspecifyVersion}")

	// testCompilation should suffice but Java 8 will mess up reflection then
	testImplementation("org.jspecify:jspecify:${jspecifyVersion}")
}

tasks.withType(Javadoc) {
	options.addStringOption('Xdoclint:none', '-quiet')
}

// Enable to get more compiler warnings.
//	tasks.withType(JavaCompile) {
//		options.compilerArgs << '-Xlint:unchecked'
//		options.deprecation = true
//	}
