plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
	// See https://github.com/vlsi/vlsi-release-plugins/blob/master/plugins/gradle-extensions-plugin/README.md
	id 'com.github.vlsi.gradle-extensions'
}

repositories {
	mavenCentral()
	//maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
}

group = moduleName
version = jqwikVersion

tasks['publish'].dependsOn(build)

publishing {
	repositories {
		maven {
			// hint: credentials are in ~/.gradle/gradle.properties
			// Since June 2024 Sonatype seems to require a token for publishing
			def repoUsername = project.hasProperty('tokenUsername') ? project.tokenUsername : ''
			def repoPassword = project.hasProperty('tokenPassword') ? project.tokenPassword : ''

			//def repoUsername = project.hasProperty('sonatypeUsername') ? project.sonatypeUsername : ''
			//def repoPassword = project.hasProperty('sonatypePassword') ? project.sonatype : ''

			credentials {
				username = repoUsername
				password = repoPassword
			}

			// Documentation for new publishing mechanism: https://central.sonatype.org/publish/publish-portal-ossrh-staging-api/
			// URLs suggested in https://central.sonatype.org/publish/publish-portal-ossrh-staging-api/#configuring-your-plugin

			// Works but deployments don't show up in https://central.sonatype.com
			def releasesRepoUrl = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"

			// Results in 400 error:
			// def releasesRepoUrl = "https://ossrh-staging-api.central.sonatype.com/service/local/"

			// Works
			def snapshotsRepoUrl = "https://central.sonatype.com/repository/maven-snapshots/"

			url = isSnapshotRelease ? snapshotsRepoUrl : releasesRepoUrl

			// Additional requests to execute for manual deployments as suggested in documentation:
			// One of:
			// - curl -X POST https://ossrh-staging-api.central.sonatype.com/manual/upload/defaultRepository/net.jqwik
			// - curl -G -d "ip=any" -d "profile_id=net.jqwik" https://ossrh-staging-api.central.sonatype.com/manual/search/repositories
			// Sadly, both did not lead to deployment showing up on central.sonatype.com

		}
	}
}

java {
	withJavadocJar()
	withSourcesJar()
	sourceCompatibility = JavaVersion.toVersion("${javaTargetVersion}")
	targetCompatibility = JavaVersion.toVersion("${javaTargetVersion}")
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

compileTestJava {
	options.compilerArgs += '-parameters'
}

dependencies {
	api("org.apiguardian:apiguardian-api:${apiGuardianVersion}")
	compileOnly("org.jspecify:jspecify:${jspecifyVersion}")

	// testCompilation should suffice but Java 8 will mess up reflection then
	testImplementation("org.jspecify:jspecify:${jspecifyVersion}")
}

tasks.withType(Javadoc) {
	options.addStringOption('Xdoclint:none', '-quiet')
}

// Enable to get more compiler warnings.
//	tasks.withType(JavaCompile) {
//		options.compilerArgs << '-Xlint:unchecked'
//		options.deprecation = true
//	}
