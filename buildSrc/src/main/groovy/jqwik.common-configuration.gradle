plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
	// See https://github.com/vlsi/vlsi-release-plugins/blob/master/plugins/gradle-extensions-plugin/README.md
	id 'com.github.vlsi.gradle-extensions'
}

repositories {
	mavenCentral()
	//maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
}

group = moduleName
version = jqwikVersion

tasks['publish'].dependsOn(build)

publishing {
	repositories {
		maven {
			// hint: credentials are in ~/.gradle/gradle.properties
			def ossrhUsername = project.hasProperty('ossrhUsername') ? project.ossrhUsername : ''
			def ossrhPassword = project.hasProperty('ossrhPassword') ? project.ossrhPassword : ''

			credentials {
				username = ossrhUsername
				password = ossrhPassword
			}

			// change URLs to point to your repos, e.g. http://my.org/repo
			def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
			def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
			// def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
			// def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
			url = isSnapshotRelease ? snapshotsRepoUrl : releasesRepoUrl
		}
	}
}

java {
	withJavadocJar()
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

compileTestJava {
	options.compilerArgs += '-parameters'
}

dependencies {
	api("org.apiguardian:apiguardian-api:${apiGuardianVersion}")
	compileOnly("com.google.code.findbugs:jsr305:${findbugsVersion}")
	// compileOnly "org.jetbrains.kotlin:kotlin-annotations-jvm:${kotlinVersion}"
	compileOnly("org.jspecify:jspecify:${jspecifyVersion}")
	testCompileOnly("com.google.code.findbugs:jsr305:${findbugsVersion}")

	// testCompilation should suffice but Java 8 will mess up reflection then
	testImplementation("org.jspecify:jspecify:${jspecifyVersion}")
}

tasks.withType(Javadoc) {
	options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.withType(ProcessForkOptions).configureEach {
	// Prevent Gradle build cache reuse for different _JAVA_OPTIONS
	inputs.property("ENV:_JAVA_OPTIONS", providers.environmentVariable("_JAVA_OPTIONS")).optional(true)
}

tasks.withType(JavaCompile).configureEach {
	// Prevent Gradle build cache reuse for different _JAVA_OPTIONS
	inputs.property("ENV:_JAVA_OPTIONS", providers.environmentVariable("_JAVA_OPTIONS")).optional(true)
}

tasks.withType(Test).configureEach {
	def language = System.getProperty("user.language") ?: "TR"
	if (language != null) {
		systemProperty("user.language", language)
	}
	def country = System.getProperty("user.country") ?: "tr"
	if (country != null) {
		systemProperty("user.country", country)
	}
	// Tests might depend on timezone, so treat it as an input
	inputs.property("ENV:TZ", providers.environmentVariable("TZ")).optional(true)

	def testExtraJvmArgs = project.findProperty("testExtraJvmArgs")
	if (testExtraJvmArgs instanceof String && !testExtraJvmArgs.isEmpty()) {
		jvmArgs(testExtraJvmArgs.split(" ::: "))
	}
}

// Enable to get more compiler warnings.
//	tasks.withType(JavaCompile) {
//		options.compilerArgs << '-Xlint:unchecked'
//		options.deprecation = true
//	}
